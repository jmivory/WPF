Web View:
  Name:               JMIAdminCustomerContactsListView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Admin Cust Contacts List View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Height(904) Width(1700) Verticalscroll(True) Layoutmanager(#MainDesktopLayout) Componenttag('JMICDC001')
    
    * BPF Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    
    * JMI Application Objects
    * Language Support
    Define_Com Class(#JMIAdminCustomerContactsListViewStrings) Name(#Strings)
    
    * Reference for JMI Data objects
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    Define_Com Class(#JMICustomerContactsDataClient) Name(#CustContacts) Scope(*APPLICATION)
    
    Define_Com Class(#JMICustomerContactClass) Name(#currentcontact) Reference(*DYNAMIC)
    
    * Panel Styles
    Define_Com Class(#PRIM_VS.Style) Name(#ListRoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#LoadMoreDisplayStyle) Fontweight(Weight500)
    
    * Main Panel Layouts
    
    * Desktop
    Define_Com Class(#PRIM_TBLO) Name(#MainDesktopLayout) Margintop(24) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn1) Displayposition(1) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn2) Displayposition(2) Parent(#MainDesktopLayout) Width(98) Minwidth(280) Maxwidth(840)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn5) Displayposition(3) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow1) Displayposition(1) Parent(#MainDesktopLayout) Height(276) Units(Content) Minheight(220)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow2) Displayposition(2) Parent(#MainDesktopLayout) Height(484) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow4) Displayposition(3) Parent(#MainDesktopLayout) Units(Pixels) Height(120)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem1) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Avatar) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem2) Column(#MainDesktopLayoutColumn2) Manage(#PanelTitle) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Alignment(TopCenter) Margintop(17) Marginleft(24) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem3) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#PanelExplanation) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Marginleft(24) Marginright(24) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem8) Column(#MainDesktopLayoutColumn2) Manage(#FooterText) Parent(#MainDesktopLayout) Sizing(ContentHeightFitToWidth) Alignment(TopCenter) Row(#MainDesktopLayoutRow4) Marginleft(24) Marginright(24) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem5) Alignment(TopLeft) Column(#MainDesktopLayoutColumn2) Manage(#PanelNavigationIcon) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem4) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Search) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24) Marginright(2) Marginleft(2)
    
    * List Layout
    Define_Com Class(#PRIM_TBLO) Name(#CustContactListLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CustContactListLayoutColumn1) Displayposition(1) Parent(#CustContactListLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CustContactListLayoutRow1) Displayposition(1) Parent(#CustContactListLayout)
    
    * List Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactListLayoutItem1) Alignment(CenterLeft) Column(#CustContactListLayoutColumn1) Manage(#ContactListIcon) Parent(#CustContactListLayout) Row(#CustContactListLayoutRow1) Sizing(ContentWidthAndHeight) Marginleft(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactListLayoutItem2) Alignment(CenterLeft) Column(#CustContactListLayoutColumn1) Flow(CenterVertical) Manage(#ContactListPrimaryText) Marginleft(80) Parent(#CustContactListLayout) Row(#CustContactListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactListLayoutItem3) Alignment(CenterLeft) Column(#CustContactListLayoutColumn1) Flow(CenterVertical) Manage(#ContactListSecondaryText) Marginleft(80) Parent(#CustContactListLayout) Row(#CustContactListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactListLayoutItem5) Alignment(CenterRight) Column(#CustContactListLayoutColumn1) Manage(#ContactListStatusText) Parent(#CustContactListLayout) Row(#CustContactListLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Left) Marginright(24) Marginbottom(10)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem6) Manage(#CustContactPanel) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow2) Sizing(ContentHeightFitToWidth) Column(#MainDesktopLayoutColumn2) Alignment(TopLeft) Margintop(24) Flow(Down)
    
    * Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#CustContactPanelLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CustContactPanelLayoutColumn1) Displayposition(1) Parent(#CustContactPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CustContactPanelLayoutColumn2) Displayposition(2) Parent(#CustContactPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#CustContactPanelLayoutColumn3) Displayposition(3) Parent(#CustContactPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CustContactPanelLayoutRow1) Displayposition(1) Parent(#CustContactPanelLayout) Height(62) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CustContactPanelLayoutRow2) Displayposition(2) Parent(#CustContactPanelLayout) Height(316) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#CustContactPanelLayoutRow3) Displayposition(3) Parent(#CustContactPanelLayout) Height(80) Units(Content) Minheight(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactPanelLayoutItem1) Manage(#ListFilterTabs) Parent(#CustContactPanelLayout) Row(#CustContactPanelLayoutRow1) Sizing(FitToWidth) Column(#CustContactPanelLayoutColumn2) Margintop(14) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactPanelLayoutItem2) Alignment(BottomLeft) Column(#CustContactPanelLayoutColumn1) Manage(#Divider) Parent(#CustContactPanelLayout) Row(#CustContactPanelLayoutRow1) Sizing(FitToWidth) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactPanelLayoutItem4) Alignment(TopLeft) Column(#CustContactPanelLayoutColumn1) Manage(#ContactList) Parent(#CustContactPanelLayout) Row(#CustContactPanelLayoutRow2) Columnspan(3) Margintop(24) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#CustContactPanelLayoutItem5) Manage(#ContactListActionLabel) Parent(#CustContactPanelLayout) Row(#CustContactPanelLayoutRow3) Column(#CustContactPanelLayoutColumn1) Alignment(TopLeft) Columnspan(3) Sizing(FitToWidth) Margintop(24)
    
    * Panel Controls
    Define_Com Class(#PRIM_MD.Icon) Name(#PanelNavigationIcon) Displayposition(1) Icon('arrow_back') Left(430) Parent(#COM_OWNER) Tabposition(6) Top(24) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Icon) Name(#Avatar) Displayposition(2) Height(100) Icon('supervisor_account') Left(800) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('MediumTitle+Rounded') Top(24) Width(100) Iconheight(80) Iconpadding(10)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelTitle) Caption('Manage Customer Contacts') Displayposition(3) Left(454) Parent(#COM_OWNER) Tabposition(3) Themedrawstyle('Title') Top(141) Width(792) Captionalignment(Center) Height(47) Wordwrap(True)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelExplanation) Displayposition(4) Left(454) Parent(#COM_OWNER) Tabposition(4) Themedrawstyle('ForegroundTheme+Heading3') Top(204) Height(24) Width(792) Wordwrap(True) Captionalignment(Center) Caption('To manage a contact, first find and then select the contact')
    
    * Search Field
    Define_Com Class(#PRIM_MD.Edit) Name(#Search) Appearance(EditBox) Captionposition(None) Displayposition(5) Height(48) Helperposition(None) Icon('search') Left(432) Parent(#COM_OWNER) Placeholder('Search for user by name....') Tabposition(1) Top(252) Width(836) Style(#RoundedCornerStyle) Themedrawstyle('Text(48,48,48,1)')
    
    * Cards
    Define_Com Class(#PRIM_PANL) Name(#CustContactPanel) Displayposition(7) Height(460) Left(430) Parent(#COM_OWNER) Tabposition(7) Tabstop(False) Top(324) Width(840) Themedrawstyle('Card') Style(#ListRoundedCornerStyle) Layoutmanager(#CustContactPanelLayout)
    
    * Filter Tabs
    Define_Com Class(#PRIM_MD.Tab) Name(#ListFilterTabs) Displayposition(1) Height(50) Parent(#CustContactPanel) Tabposition(1) Width(790) Style(#NavigationLinkStyle) Left(24) Top(14)
    
    * Contacts List
    Define_Com Class(#PRIM_MD.List) Name(#ContactList) Displayposition(3) Layoutmanager(#CustContactListLayout) Left(0) Parent(#CustContactPanel) Rowheight(72) Tabposition(3) Top(86) Height(292) Width(838)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContactListIcon) Displayposition(1) Icon('account_circle') Left(24) Parent(#ContactList) Tabposition(1) Themedrawstyle('ForegroundMediumPrimary') Top(18) Width(36) Iconheight(36) Iconalignment(AboveCaption) Captionalignment(Center)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContactListPrimaryText) Caption('Gregory Sippel') Displayposition(4) Height(24) Left(80) Parent(#ContactList) Tabposition(3) Themedrawstyle('Heading3') Top(14) Width(734) Wordwrap(True)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContactListSecondaryText) Displayposition(5) Height(21) Left(80) Parent(#ContactList) Tabposition(4) Themedrawstyle('ForegroundTheme') Top(38) Width(734) Caption('greg.sippel@lansa.com.au')
    Define_Com Class(#PRIM_MD.ListLabel) Name(#ContactListStatusText) Caption('Active') Displayposition(2) Dragstyle(None) Left(771) Parent(#ContactList) Tabposition(5) Top(16) Height(21) Width(43) Iconalignment(AfterCaption)
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(2) Height(1) Left(0) Parent(#CustContactPanel) Tabposition(2) Tabstop(False) Themedrawstyle('TopDivider') Top(61) Width(838)
    
    * List Action Label
    Define_Com Class(#PRIM_MD.Label) Name(#ContactListActionLabel) Caption('Load more') Displayposition(4) Left(0) Parent(#CustContactPanel) Tabposition(4) Themedrawstyle('ForegroundMediumPrimary') Top(402) Height(56) Width(838) Style(#LoadMoreDisplayStyle) Captionalignment(Center) Componenttag('LOAD_MORE')
    
    * Footer
    Define_Com Class(#PRIM_MD.Label) Name(#FooterText) Captionalignment(TopCenter) Displayposition(6) Height(45) Parent(#COM_OWNER) Tabposition(5) Top(808) Width(792) Wordwrap(True) Caption('The settings here can affect the other users, consider your changes carefully') Paddingbottom(24) Left(454)
    
    * References
    
    * Global Variables
    Define_Com Class(#prim_boln) Name(#MoreResults)
    Define_Com Class(#PRIM_TIMR) Name(#DrawDelay) Interval(25) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ListEntryDelay) Interval(1) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ScrollDelay) Interval(5) Startup(Manual)
    
    Define_Com Class(#PRIM_TREE.TreeItem) Name(#MouseOverItem) Reference(*DYNAMIC)
    Define_Com Class(#prim_nmbr) Name(#CurrentScrollPos)
    
    * Filter Items
    Define_Com Class(#Prim_Lcol<#Prim_md.TabItem>) Name(#FilterItems)
    Define_Com Class(#PRIM_ALPH) Name(#CurrentFilter)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Persist the view
    #Navigate.CurrentEvent.PersistView := True
    
    If (#SYS_WEB.URLParameters<"VALUE"> *IsNot *Null)
    #Search := #SYS_WEB.URLParameters<"VALUE">.Value
    Endif
    
    If (#SYS_WEB.URLParameters<"FILTER"> *IsNot *Null)
    #CurrentFilter := #SYS_WEB.URLParameters<"FILTER">.Value
    Endif
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Set Menu Display
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetStrings
    
    #COM_SELF.SetListActionForLoading
    
    #CustContacts.GetCustContacts( #Search )
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Prepare)
    
    #Data.KeepDataClient( #COM_SELF )
    
    * #COM_SELF.CollectFilterItems
    
    Endroutine
    
    * Panel Navigation
    Evtroutine Handling(#PanelNavigationIcon.Click)
    
    * Back to Settings
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Search
    Evtroutine Handling(#Search.GotFocus)
    
    #Search.PromptIcon := 'close'
    
    Endroutine
    
    Evtroutine Handling(#Search.Prompting)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #Search.Value := *null
    #CustContacts.GetCustContacts( #Search )
    
    Endroutine
    
    Evtroutine Handling(#Search.LostFocus)
    
    #Search.PromptIcon := *null
    
    Endroutine
    
    Evtroutine Handling(#Search.Enter #Search.IconClick)
    
    If (#COM_SELF.Visible)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.LowerCase + '&value=' + #Search + '&filter=' + #CurrentFilter) False )
    
    * Test
    
    #CustContacts.GetCustContacts( #Search )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    If ((#COM_SELF.Visible) *AndIf (#ContactList.Items.IsEmpty.IsFalse))
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #CustContacts.GetCustContacts( #Search )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#FilterItems<>.Click) Com_Sender(#Sender)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #FilterItems<>.View <= *NULL
    #Sender.View <= #COM_SELF
    
    #CurrentFilter := #Sender.ComponentTag
    
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.LowerCase + '&value=' + #Search + '&filter=' + #CurrentFilter) False )
    
    #CustContacts.GetCustContacts( #Search )
    
    Endroutine
    
    * Data
    Evtroutine Handling(#CustContacts.OnGetCustomerContactsCompleted) Moreresult(#More)
    
    #MoreResults := #More
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    Evtroutine Handling(#CustContacts.SetContactUpdated) Contact(#Updated)
    #sys_web.Alert( 'ListView SetContactUpdated' )
    
    #COM_SELF.UpdateContactInList( #Updated )
    
    Endroutine
    
    * Timers
    Evtroutine Handling(#DrawDelay.Tick)
    
    #DrawDelay.Stop
    
    #COM_SELF.SetListActionAfterLoad
    
    * #ContactList.Items<1>.Focus := True
    
    #BPFPortal.ViewsPanel.SetWaitingState( "OFF" )
    
    Endroutine
    
    * Okay, the next 4 events are here to deal with list jumping in IE and Chromiumn based browsers
    * It should noted that Firefox actually works as intended, thats a first. So the issue is the list jumps
    * due to focus suddenly being paid attention to. This causes scrolling which we can see. So if a sudden
    * scroll happen this code causes it to be treated as a click. After reading this, have a "drink".
    Evtroutine Handling(#COM_OWNER.ScrollChanged) Com_Sender(#sender)
    
    If ((#Session.IsFireFox))
    Return
    Endif
    
    If ((#MouseOverItem *IsNot *Null) *And ((#CurrentScrollPos < 400) *Or (#Session.IsIE)) *And ((#COM_SELF.VerticalScrollPos > 407) *And (#COM_SELF.VerticalScrollPos < 410)))
    
    #MouseOverItem <= *null
    #COM_SELF.VerticalScrollPos := #CurrentScrollPos
    
    #Navigate.To Eventname("CUSTOMERCONTACTVIEW") Value((#ContactList.Items<#ListEntry>.RelatedReference *As #JMICustomerContactClass).ContactID.AsDisplayString)
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#ContactList.ItemMouseEnter) Item(#Item)
    
    #MouseOverItem <= #Item
    #ListEntryDelay.Start
    
    #CurrentScrollPos := #COM_SELF.VerticalScrollPos
    
    Endroutine
    
    Evtroutine Handling(#ListEntryDelay.Tick)
    
    #ListEntryDelay.Stop
    
    If (#MouseOverItem *IsNot *Null)
    #ListEntry := #MouseOverItem.Entry
    Endif
    
    Endroutine
    
    Evtroutine Handling(#ContactList.MouseLeave)
    
    #MouseOverItem <= *null
    #ListEntry := 0
    
    Endroutine
    
    Evtroutine Handling(#ContactList.ItemClick)
    
    
    #Navigate.To Eventname("CUSTOMERCONTACTVIEW") Value((#ContactList.CurrentItem.RelatedReference *As #JMICustomerContactClass).ContactID.AsString)
    
    
    Endroutine
    
    
    
    Evtroutine Handling(#ContactListActionLabel.Click)
    
    * Action only a load more situation
    If (#ContactListActionLabel.ComponentTag.UpperCase <> 'LOAD_MORE')
    Return
    Endif
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    * Do not clear list
    #COM_SELF.SetListActionForLoading( False )
    
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PanelTitle.Caption := #Strings.Text<PANELTITLE.CAPTION>
    #PanelExplanation.Caption := #Strings.Text<PANELEXPLANATION.CAPTION>
    #Search.Placeholder := #Strings.Text<SEARCH.PLACEHOLDER>
    
    #FooterText.Caption := #Strings.Text<FOOTERTEXT.CAPTION>
    
    Endroutine
    
    
    Mthroutine Name(HandleBuildList) Access(*PRIVATE)
    
    * List Control
    Define_Com Class(#PRIM_NMBR) Name(#UpperIndex)
    Define_Com Class(#PRIM_NMBR) Name(#Index)
    
    Clr_List Named(#ContactList)
    * Back to for loop
    For Each(#Item) In(#CustContacts.CustContacts)
    
    
    #Index := #Index + 1
    * IF Index LE current list items count - get next cust contact
    Continue If(#Index *LE #ContactList.Items.ItemCount)
    * add the next 25 entries to contacts list
    #COM_SELF.AddContactToList( #Item )
    
    #UpperIndex := #UpperIndex + 1
    Leave If(#UpperIndex = 25)
    
    Endfor
    
    * Set Message
    #DrawDelay.Start
    
    Endroutine
    
    Mthroutine Name(AddContactToList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#JMICustomerContactClass) Name(#Item) Pass(*BY_REFERENCE)
    
    * Add new Item to list
    Add_Entry To_List(#ContactList)
    
    * Bind the data to the item
    #ContactList.CurrentItem.RelatedReference <= #Item
    
    
    * Set Primary & Secondary Text
    #ContactListPrimaryText.CurrentItem.Caption := #Item.ContactFirstName + ' ' + #Item.ContactLastName
    #ContactListSecondaryText.CurrentItem.Caption := #Item.ContactEmailAddress
    
    Endroutine
    
    Mthroutine Name(UpdateContactInList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#JMICustomerContactClass) Name(#Item) Pass(*BY_REFERENCE)
    
    #sys_web.Alert( 'UpdateContactInList ' )
    * Get Current Entry
    Get_Entry Number(#ContactList.FindReference( #Item ).Entry) From_List(#ContactList)
    
    If_Status Is(*OKAY)
    
    * Set Primary & Secondary Text
    #ContactListPrimaryText.CurrentItem.Caption := #Item.ContactFirstName + ' ' + #Item.ContactLastName
    #ContactListSecondaryText.CurrentItem.Caption := #Item.ContactEmailAddress
    
    * Set Status Text
    #ContactListStatusText.CurrentItem.Visible := (#SYS_WEB.Device = Desktop)
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionForLoading) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#ClearList) Mandatory(True)
    
    If (#ClearList)
    
    * Clear
    Clr_List Named(#ContactList)
    
    Endif
    
    * show loading list message
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<CONTACTLISTACTIONLABEL.CAPTION.LOAD> )
    
    Endroutine
    
    Mthroutine Name(SetListActionAfterLoad)
    
    * If we have more results to load
    If (#ContactList.Items.ItemCount < #CustContacts.CustContacts.ItemCount)
    
    * We show loading more
    #COM_SELF.SetListActionDisplay( True #LoadMoreDisplayStyle "FOREGROUNDMEDIUMPRIMARY" #Strings.Text<CONTACTLISTACTIONLABEL.CAPTION.MORE> 'LOAD_MORE' )
    
    Endif
    
    * if we have no more results to load
    If (#ContactList.Items.ItemCount = #CustContacts.CustContacts.ItemCount)
    
    * Do we have more results on the server
    If (#MoreResults)
    
    * We show Refine search
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<CONTACTLISTACTIONLABEL.CAPTION.FULL> )
    
    Else
    
    If (#ContactList.Items.ItemCount > 0)
    
    * No more results on server, so no message needed
    #ContactListActionLabel.Visible := False
    
    Else
    
    * Needs correction ======================
    * No Results found
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<CONTACTLISTACTIONLABEL.CAPTION.EMPTY> )
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionDisplay) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#Visible)
    Define_Map For(*INPUT) Class(#prim_vs.Style) Name(#Style) Mandatory(*NULL) Pass(*BY_REFERENCE)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#DrawStyle) Mandatory(*NULL)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Message) Mandatory(*NULL)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Tag) Mandatory(*NULL)
    
    If (#Visible)
    
    * Set Style
    #ContactListActionLabel.Style <= #Style
    #ContactListActionLabel.ThemeDrawStyle := #DrawStyle
    
    * Set Text
    #ContactListActionLabel := #Message
    #ContactListActionLabel.ComponentTag := #Tag
    
    * Ensure we can see Action Label
    #ContactListActionLabel.Visible := True
    
    Else
    
    * If not visible then just hide
    #ContactListActionLabel.Visible := False
    
    Endif
    
    Endroutine
    
    Mthroutine Name(CollectFilterItems) Access(*PRIVATE)
    
    For Each(#control) In(#ListFilterTabs.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.TabItem)
    
    *
    If (#control.ComponentTag.LowerCase = #CurrentFilter.LowerCase)
    
    #control.View <= #Com_Self
    
    Endif
    
    #FilterItems.Insert( #control )
    
    Endfor
    
    Endroutine
    
    End_Com
