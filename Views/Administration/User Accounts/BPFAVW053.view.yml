Web View:
  Name:               BPFAdminUserAccountsListView
  Enabled For RDMLX:  Yes

  Multilingual Details:
  - ENG:
      Description:        "Portal Admin User Accounts List View"

  Source: |
    Begin_Com Role(*EXTENDS #PRIM_VIEW) Displayposition(1) Tabposition(1) Height(904) Width(1700) Verticalscroll(True) Layoutmanager(#MainDesktopLayout) Componenttag('BPFAPP008')
    
    * Reference Application Objects
    Define_Com Class(#BPFSessionManager) Name(#Session) Scope(*APPLICATION)
    Define_Com Class(#BPFNavigationManager) Name(#Navigate) Scope(*APPLICATION)
    Define_Com Class(#BPFDataClientManager) Name(#Data) Scope(*APPLICATION)
    
    * Language Support
    Define_Com Class(#BPFAdminUserAccountsListViewStrings) Name(#Strings)
    
    * Reference for Data client
    Define_Com Class(#BPFUserAccountAdminDataClient) Name(#UserAccounts) Reference(*DYNAMIC)
    
    * Panel Styles
    Define_Com Class(#PRIM_VS.Style) Name(#ListRoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#NavigationLinkStyle) Cursor(Hand)
    Define_Com Class(#PRIM_VS.Style) Name(#RoundedCornerStyle) Cornerbottomleft(8) Cornerbottomright(8) Cornertopleft(8) Cornertopright(8) Borderleft(1) Bordertop(1) Borderright(1) Borderbottom(1)
    Define_Com Class(#PRIM_VS.Style) Name(#LoadMoreDisplayStyle) Fontweight(Weight500)
    
    * Main Panel Layouts
    
    * Desktop
    Define_Com Class(#PRIM_TBLO) Name(#MainDesktopLayout) Margintop(24) Sizing(ContentHeight)
    
    * Rows and Columns
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn1) Displayposition(1) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn2) Displayposition(2) Parent(#MainDesktopLayout) Width(98) Minwidth(280) Maxwidth(840)
    Define_Com Class(#PRIM_TBLO.Column) Name(#MainDesktopLayoutColumn5) Displayposition(3) Parent(#MainDesktopLayout) Minwidth(8)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow1) Displayposition(1) Parent(#MainDesktopLayout) Height(276) Units(Content) Minheight(220)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow2) Displayposition(2) Parent(#MainDesktopLayout) Height(484) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#MainDesktopLayoutRow4) Displayposition(3) Parent(#MainDesktopLayout) Units(Pixels) Height(120)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem1) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Avatar) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem2) Column(#MainDesktopLayoutColumn2) Manage(#PanelTitle) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Flow(Down) Alignment(TopCenter) Margintop(17) Marginleft(24) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem3) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#PanelExplanation) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(ContentHeightFitToWidth) Marginleft(24) Marginright(24) Flow(Down) Margintop(16)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem8) Column(#MainDesktopLayoutColumn2) Manage(#FooterText) Parent(#MainDesktopLayout) Sizing(ContentHeightFitToWidth) Alignment(TopCenter) Row(#MainDesktopLayoutRow4) Marginleft(24) Marginright(24) Margintop(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem5) Alignment(TopLeft) Column(#MainDesktopLayoutColumn2) Manage(#PanelNavigationIcon) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(None)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem4) Alignment(TopCenter) Column(#MainDesktopLayoutColumn2) Manage(#Search) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow1) Sizing(FitToWidth) Flow(Down) Margintop(24) Marginright(2) Marginleft(2)
    
    * List Layout
    Define_Com Class(#PRIM_TBLO) Name(#UserAccountListLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#UserAccountListLayoutColumn1) Displayposition(1) Parent(#UserAccountListLayout)
    Define_Com Class(#PRIM_TBLO.Row) Name(#UserAccountListLayoutRow1) Displayposition(1) Parent(#UserAccountListLayout)
    
    * List Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountListLayoutItem1) Alignment(CenterLeft) Column(#UserAccountListLayoutColumn1) Manage(#AccountListIcon) Parent(#UserAccountListLayout) Row(#UserAccountListLayoutRow1) Sizing(ContentWidthAndHeight) Marginleft(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountListLayoutItem2) Alignment(CenterLeft) Column(#UserAccountListLayoutColumn1) Flow(CenterVertical) Manage(#AccountListPrimaryText) Marginleft(80) Parent(#UserAccountListLayout) Row(#UserAccountListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountListLayoutItem3) Alignment(CenterLeft) Column(#UserAccountListLayoutColumn1) Flow(CenterVertical) Manage(#AccountListSecondaryText) Marginleft(80) Parent(#UserAccountListLayout) Row(#UserAccountListLayoutRow1) Sizing(ContentHeightFitToWidth) Marginright(24)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountListLayoutItem5) Alignment(CenterRight) Column(#UserAccountListLayoutColumn1) Manage(#AccountListStatusText) Parent(#UserAccountListLayout) Row(#UserAccountListLayoutRow1) Sizing(ContentWidthAndHeight) Flow(Left) Marginright(24) Marginbottom(10)
    Define_Com Class(#PRIM_TBLO.Item) Name(#MainDesktopLayoutItem6) Manage(#UserAccountPanel) Parent(#MainDesktopLayout) Row(#MainDesktopLayoutRow2) Sizing(ContentHeightFitToWidth) Column(#MainDesktopLayoutColumn2) Alignment(TopLeft) Margintop(24) Flow(Down)
    
    * Panel Layout
    Define_Com Class(#PRIM_TBLO) Name(#UserAccountPanelLayout)
    Define_Com Class(#PRIM_TBLO.Column) Name(#UserAccountPanelLayoutColumn1) Displayposition(1) Parent(#UserAccountPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Column) Name(#UserAccountPanelLayoutColumn2) Displayposition(2) Parent(#UserAccountPanelLayout) Width(98)
    Define_Com Class(#PRIM_TBLO.Column) Name(#UserAccountPanelLayoutColumn3) Displayposition(3) Parent(#UserAccountPanelLayout) Minwidth(24)
    Define_Com Class(#PRIM_TBLO.Row) Name(#UserAccountPanelLayoutRow1) Displayposition(1) Parent(#UserAccountPanelLayout) Height(62) Units(Pixels)
    Define_Com Class(#PRIM_TBLO.Row) Name(#UserAccountPanelLayoutRow2) Displayposition(2) Parent(#UserAccountPanelLayout) Height(316) Units(Content)
    Define_Com Class(#PRIM_TBLO.Row) Name(#UserAccountPanelLayoutRow3) Displayposition(3) Parent(#UserAccountPanelLayout) Height(80) Units(Content) Minheight(24)
    
    * Layout Items
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountPanelLayoutItem1) Manage(#ListFilterTabs) Parent(#UserAccountPanelLayout) Row(#UserAccountPanelLayoutRow1) Sizing(FitToWidth) Column(#UserAccountPanelLayoutColumn2) Margintop(14) Alignment(TopLeft) Flow(Down)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountPanelLayoutItem2) Alignment(BottomLeft) Column(#UserAccountPanelLayoutColumn1) Manage(#Divider) Parent(#UserAccountPanelLayout) Row(#UserAccountPanelLayoutRow1) Sizing(FitToWidth) Columnspan(3)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountPanelLayoutItem4) Alignment(TopLeft) Column(#UserAccountPanelLayoutColumn1) Manage(#AccountList) Parent(#UserAccountPanelLayout) Row(#UserAccountPanelLayoutRow2) Columnspan(3) Margintop(24) Sizing(ContentHeightFitToWidth)
    Define_Com Class(#PRIM_TBLO.Item) Name(#UserAccountPanelLayoutItem5) Manage(#AccountListActionLabel) Parent(#UserAccountPanelLayout) Row(#UserAccountPanelLayoutRow3) Column(#UserAccountPanelLayoutColumn1) Alignment(TopLeft) Columnspan(3) Sizing(FitToWidth) Margintop(24)
    
    * Panel Controls
    Define_Com Class(#PRIM_MD.Icon) Name(#PanelNavigationIcon) Displayposition(1) Icon('arrow_back') Left(430) Parent(#COM_OWNER) Tabposition(6) Top(24) Style(#NavigationLinkStyle)
    Define_Com Class(#PRIM_MD.Icon) Name(#Avatar) Displayposition(2) Height(100) Icon('supervisor_account') Left(800) Parent(#COM_OWNER) Tabposition(2) Themedrawstyle('MediumTitle+Rounded') Top(24) Width(100) Iconheight(80) Iconpadding(10)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelTitle) Caption('Manage User Accounts') Displayposition(3) Left(454) Parent(#COM_OWNER) Tabposition(3) Themedrawstyle('Title') Top(141) Width(792) Captionalignment(Center) Height(47) Wordwrap(True)
    Define_Com Class(#PRIM_MD.Label) Name(#PanelExplanation) Displayposition(4) Left(454) Parent(#COM_OWNER) Tabposition(4) Themedrawstyle('ForegroundTheme+Heading3') Top(204) Height(24) Width(792) Wordwrap(True) Captionalignment(Center) Caption('To manage a user account first find and then select the user account')
    
    * Search Field
    Define_Com Class(#PRIM_MD.Edit) Name(#Search) Appearance(EditBox) Captionposition(None) Displayposition(5) Height(48) Helperposition(None) Icon('search') Left(432) Parent(#COM_OWNER) Placeholder('Search for user by name or email...') Tabposition(1) Top(252) Width(836) Style(#RoundedCornerStyle) Themedrawstyle('Text(48,48,48,1)')
    
    * Cards
    Define_Com Class(#PRIM_PANL) Name(#UserAccountPanel) Displayposition(7) Height(460) Left(430) Parent(#COM_OWNER) Tabposition(7) Tabstop(False) Top(324) Width(840) Themedrawstyle('Card') Style(#ListRoundedCornerStyle) Layoutmanager(#UserAccountPanelLayout)
    
    * Filter Tabs
    Define_Com Class(#PRIM_MD.Tab) Name(#ListFilterTabs) Displayposition(1) Height(50) Parent(#UserAccountPanel) Tabposition(1) Width(790) Style(#NavigationLinkStyle) Left(24) Top(14)
    Define_Com Class(#PRIM_MD.TabItem) Name(#AllTabItem) Caption('All') Displayposition(1) Height(50) Left(0) Parent(#ListFilterTabs) Tabposition(2) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(114) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER)
    Define_Com Class(#PRIM_MD.TabItem) Name(#ActiveTabItem) Caption('Active') Displayposition(2) Height(50) Left(114) Parent(#ListFilterTabs) Tabposition(5) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(112) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('Active')
    Define_Com Class(#PRIM_MD.TabItem) Name(#DisabledTabItem) Caption('Disabled') Displayposition(3) Height(50) Left(226) Parent(#ListFilterTabs) Tabposition(1) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(112) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('Disabled')
    Define_Com Class(#PRIM_MD.TabItem) Name(#ResetTabItem) Caption('Reset') Displayposition(4) Height(50) Left(338) Parent(#ListFilterTabs) Tabposition(6) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(114) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('Reset')
    Define_Com Class(#PRIM_MD.TabItem) Name(#NewTabItem) Caption('New') Displayposition(5) Height(50) Left(452) Parent(#ListFilterTabs) Tabposition(7) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(114) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('New')
    Define_Com Class(#PRIM_MD.TabItem) Name(#PendingTabItem) Caption('Pending') Displayposition(6) Height(50) Left(566) Parent(#ListFilterTabs) Tabposition(3) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(112) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('Pending')
    Define_Com Class(#PRIM_MD.TabItem) Name(#RemoveTabItem) Caption('Removed') Displayposition(7) Height(50) Left(678) Parent(#ListFilterTabs) Tabposition(4) Top(0) Themedrawstyle('Heading3+ForegroundTheme500') Width(112) Captionmarginbottom(0) Captionmargintop(0) View(#COM_OWNER) Componenttag('Removed')
    
    * Account List
    Define_Com Class(#PRIM_MD.List) Name(#AccountList) Displayposition(3) Layoutmanager(#UserAccountListLayout) Left(0) Parent(#UserAccountPanel) Rowheight(72) Tabposition(3) Top(86) Height(292) Width(838)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#AccountListIcon) Displayposition(1) Icon('account_circle') Left(24) Parent(#AccountList) Tabposition(1) Themedrawstyle('ForegroundMediumPrimary') Top(18) Width(36) Iconheight(36) Iconalignment(AboveCaption) Captionalignment(Center)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#AccountListPrimaryText) Caption('Gregory Sippel') Displayposition(4) Height(24) Left(80) Parent(#AccountList) Tabposition(3) Themedrawstyle('Heading3') Top(14) Width(734) Wordwrap(True)
    Define_Com Class(#PRIM_MD.ListLabel) Name(#AccountListSecondaryText) Displayposition(5) Height(21) Left(80) Parent(#AccountList) Tabposition(4) Themedrawstyle('ForegroundTheme') Top(38) Width(734) Caption('greg.sippel@lansa.com.au')
    Define_Com Class(#PRIM_MD.ListLabel) Name(#AccountListStatusText) Caption('Active') Displayposition(2) Dragstyle(None) Left(771) Parent(#AccountList) Tabposition(5) Top(16) Height(21) Width(43) Iconalignment(AfterCaption)
    Define_Com Class(#PRIM_PANL) Name(#Divider) Displayposition(2) Height(1) Left(0) Parent(#UserAccountPanel) Tabposition(2) Tabstop(False) Themedrawstyle('TopDivider') Top(61) Width(838)
    
    * List Action Label
    Define_Com Class(#PRIM_MD.Label) Name(#AccountListActionLabel) Caption('Load more') Displayposition(4) Left(0) Parent(#UserAccountPanel) Tabposition(4) Themedrawstyle('ForegroundMediumPrimary') Top(402) Height(56) Width(838) Style(#LoadMoreDisplayStyle) Captionalignment(Center)
    
    * Footer
    Define_Com Class(#PRIM_MD.Label) Name(#FooterText) Captionalignment(TopCenter) Displayposition(6) Height(45) Parent(#COM_OWNER) Tabposition(5) Top(808) Width(792) Wordwrap(True) Caption('The settings here can affect the security of user accounts, consider your changes carefully') Paddingbottom(24) Left(454)
    
    * References
    
    * Global Variables
    Define_Com Class(#prim_boln) Name(#MoreResults)
    Define_Com Class(#PRIM_TIMR) Name(#DrawDelay) Interval(25) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ListEntryDelay) Interval(1) Startup(Manual)
    Define_Com Class(#PRIM_TIMR) Name(#ScrollDelay) Interval(5) Startup(Manual)
    
    Define_Com Class(#PRIM_TREE.TreeItem) Name(#MouseOverItem) Reference(*DYNAMIC)
    Define_Com Class(#prim_nmbr) Name(#CurrentScrollPos)
    
    * Filter Items
    Define_Com Class(#Prim_Lcol<#Prim_md.TabItem>) Name(#FilterItems)
    Define_Com Class(#PRIM_ALPH) Name(#CurrentFilter)
    
    * Routines
    
    * Events
    
    * Self
    
    Evtroutine Handling(#COM_OWNER.CreateInstance)
    
    * Get References
    #UserAccounts <= (#Data.GetDataClient( #COM_SELF ) *As #BPFUserAccountAdminDataClient)
    
    * Persist the view
    #Navigate.CurrentEvent.PersistView := True
    
    If (#SYS_WEB.URLParameters<"VALUE"> *IsNot *Null)
    #Search := #SYS_WEB.URLParameters<"VALUE">.Value
    Endif
    
    If (#SYS_WEB.URLParameters<"FILTER"> *IsNot *Null)
    #CurrentFilter := #SYS_WEB.URLParameters<"FILTER">.Value
    Endif
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Initialize)
    
    * Set Menu Display
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetStrings
    
    #COM_SELF.SetListActionForLoading
    
    #UserAccounts.GetUserAccounts( #Search #CurrentFilter )
    
    Endroutine
    
    Evtroutine Handling(#COM_OWNER.Prepare)
    
    #Data.KeepDataClient( #COM_SELF )
    
    #COM_SELF.CollectFilterItems
    
    Endroutine
    
    * Panel Navigation
    Evtroutine Handling(#PanelNavigationIcon.Click)
    
    * Back to Settings
    #SYS_WEB.History.GoBack
    
    Endroutine
    
    * Search
    Evtroutine Handling(#Search.GotFocus)
    
    #Search.PromptIcon := 'close'
    
    Endroutine
    
    Evtroutine Handling(#Search.Prompting)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #Search.Value := *null
    #UserAccounts.GetUserAccounts( #Search #CurrentFilter )
    
    Endroutine
    
    Evtroutine Handling(#Search.LostFocus)
    
    #Search.PromptIcon := *null
    
    Endroutine
    
    Evtroutine Handling(#Search.Enter #Search.IconClick)
    
    If (#COM_SELF.Visible)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    * Test
    * Update the URL to store teh current search values
    * #Navigate.To Eventname(#Navigate.CurrentEvent.EventName) Value((#search + '&filter=' + #CurrentFilter)) Signalreplaceevent(False) Type(REPLACE)
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.LowerCase + '&value=' + #Search + '&filter=' + #CurrentFilter) False )
    
    * Test
    
    #UserAccounts.GetUserAccounts( #Search #CurrentFilter )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#SYS_WEB.DeviceChanged)
    
    If ((#COM_SELF.Visible) *AndIf (#AccountList.Items.IsEmpty.IsFalse))
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #UserAccounts.GetUserAccounts( #Search #CurrentFilter )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#FilterItems<>.Click) Com_Sender(#Sender)
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading
    
    #FilterItems<>.View <= *NULL
    #Sender.View <= #COM_SELF
    
    #CurrentFilter := #Sender.ComponentTag
    
    * #Navigate.To Eventname(#Navigate.CurrentEvent.EventName) Value((#search + '&filter=' + #CurrentFilter)) Signalreplaceevent(False) Type(REPLACE)
    #SYS_WEB.History.Replace( ('event=' + #Navigate.CurrentEvent.EventName.LowerCase + '&value=' + #Search + '&filter=' + #CurrentFilter) False )
    #UserAccounts.GetUserAccounts( #Search #CurrentFilter )
    
    Endroutine
    
    * Data
    Evtroutine Handling(#UserAccounts.OnGetUserAccountsCompleted) Moreresult(#More)
    
    #MoreResults := #More
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    Evtroutine Handling(#UserAccounts.OnAccountUpdated) Account(#Updated)
    
    #COM_SELF.UpdateAccountInList( #Updated )
    
    Endroutine
    
    * Timers
    Evtroutine Handling(#DrawDelay.Tick)
    
    #DrawDelay.Stop
    
    #COM_SELF.SetListActionAfterLoad
    
    * #AccountList.Items<1>.Focus := True
    
    #BPFPortal.ViewsPanel.SetWaitingState( "OFF" )
    
    Endroutine
    
    * Okay, the next 4 events are here to deal with list jumping in IE and Chromiumn based browsers
    * It should noted that Firefox actually works as intended, thats a first. So the issue is the list jumps
    * due to focus suddenly being paid attention to. This causes scrolling which we can see. So if a sudden
    * scroll happen this code causes it to be treated as a click. After reading this, have a "drink".
    Evtroutine Handling(#COM_OWNER.ScrollChanged) Com_Sender(#sender)
    
    If ((#Session.IsFireFox))
    Return
    Endif
    
    If ((#MouseOverItem *IsNot *Null) *And ((#CurrentScrollPos < 400) *Or (#Session.IsIE)) *And ((#COM_SELF.VerticalScrollPos > 407) *And (#COM_SELF.VerticalScrollPos < 410)))
    
    #MouseOverItem <= *null
    #COM_SELF.VerticalScrollPos := #CurrentScrollPos
    
    If (#Session.CurrentUser.EmailAdress <> (#AccountList.Items<#ListEntry>.RelatedReference *As #BPFUserAccountAdminItem).EmailAdress)
    
    #Navigate.To Eventname("ADMINUSERACCOUNTVIEW") Value((#AccountList.Items<#ListEntry>.RelatedReference *As #BPFUserAccountAdminItem).Identifier)
    
    Else
    
    #BPFPortal.ShowToaster( #Strings.Text<MESSAGE.ERROR.OWNACCOUNT> )
    
    Endif
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#AccountList.ItemMouseEnter) Item(#Item)
    
    #MouseOverItem <= #Item
    #ListEntryDelay.Start
    
    #CurrentScrollPos := #COM_SELF.VerticalScrollPos
    
    Endroutine
    
    Evtroutine Handling(#ListEntryDelay.Tick)
    
    #ListEntryDelay.Stop
    
    If (#MouseOverItem *IsNot *Null)
    #ListEntry := #MouseOverItem.Entry
    Endif
    
    Endroutine
    
    Evtroutine Handling(#AccountList.MouseLeave)
    
    #MouseOverItem <= *null
    #ListEntry := 0
    
    Endroutine
    
    Evtroutine Handling(#AccountList.ItemClick)
    
    If (#Session.CurrentUser.EmailAdress <> (#AccountList.CurrentItem.RelatedReference *As #BPFUserAccountAdminItem).EmailAdress)
    
    #Navigate.To Eventname("ADMINUSERACCOUNTVIEW") Value((#AccountList.CurrentItem.RelatedReference *As #BPFUserAccountAdminItem).Identifier)
    
    Else
    
    #BPFPortal.ShowToaster( #Strings.Text<MESSAGE.ERROR.OWNACCOUNT> )
    
    Endif
    
    Endroutine
    
    Evtroutine Handling(#AccountListActionLabel.Click)
    
    * Action only a load more situation
    If (#AccountListActionLabel.ComponentTag.UpperCase <> 'LOAD_MORE')
    Return
    Endif
    
    #BPFPortal.ViewsPanel.SetWaitingState( "ON" )
    #COM_SELF.SetListActionForLoading( False )
    
    #COM_SELF.HandleBuildList
    
    Endroutine
    
    * Methods
    
    Mthroutine Name(SetStrings)
    
    #PanelTitle.Caption := #Strings.Text<PANELTITLE.CAPTION>
    #PanelExplanation.Caption := #Strings.Text<PANELEXPLANATION.CAPTION>
    #Search.Placeholder := #Strings.Text<SEARCH.PLACEHOLDER>
    #AllTabItem.Caption := #Strings.Text<ALLTABITEM.CAPTION>
    #ActiveTabItem.Caption := #Strings.Text<ACTIVETABITEM.CAPTION>
    #DisabledTabItem.Caption := #Strings.Text<DISABLEDTABITEM.CAPTION>
    #ResetTabItem.Caption := #Strings.Text<RESETTABITEM.CAPTION>
    #NewTabItem.Caption := #Strings.Text<NEWTABITEM.CAPTION>
    #PendingTabItem.Caption := #Strings.Text<PENDINGTABITEM.CAPTION>
    #RemoveTabItem.Caption := #Strings.Text<REMOVEDTABITEM.CAPTION>
    #FooterText.Caption := #Strings.Text<FOOTERTEXT.CAPTION>
    
    Endroutine
    
    
    Mthroutine Name(HandleBuildList) Access(*PRIVATE)
    
    * List Control
    Define_Com Class(#PRIM_NMBR) Name(#UpperIndex)
    Define_Com Class(#PRIM_NMBR) Name(#Index)
    
    * Back to for loop
    For Each(#Item) In(#UserAccounts.UserAccounts)
    
    #Index := #Index + 1
    * If Index LE current list item count
    Continue If(#Index <= #AccountList.Items.ItemCount)
    
    #COM_SELF.AddAccountToList( #Item )
    
    #UpperIndex := #UpperIndex + 1
    Leave If(#UpperIndex = 25)
    
    Endfor
    
    * Set Message
    #DrawDelay.Start
    
    Endroutine
    
    Mthroutine Name(AddAccountToList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#BPFUserAccountAdminItem) Name(#Item) Pass(*BY_REFERENCE)
    
    * Add new Item to list
    Add_Entry To_List(#AccountList)
    
    * Bind the data to the item
    #AccountList.CurrentItem.RelatedReference <= #Item
    
    * Set the account icon
    #AccountListIcon.CurrentItem.Icon := #Item.IconName
    #AccountListIcon.CurrentItem.ThemeDrawStyle := #Item.StatusColour
    
    * Set Primary & Secondary Text
    #AccountListPrimaryText.CurrentItem.Caption := #Item.FirstName + ' ' + #Item.LastName
    #AccountListSecondaryText.CurrentItem.Caption := #Item.EmailAdress
    
    * Set Status Text
    #AccountListStatusText.CurrentItem.Visible := (#SYS_WEB.Device = Desktop)
    If (#AccountListStatusText.CurrentItem.Visible)
    
    #AccountListStatusText.CurrentItem.Caption := #Strings.Text<(#Item.Status.UpperCase + 'TABITEM.CAPTION')>
    
    If (#Item.Status <> 'Active')
    
    #AccountListStatusText.CurrentItem.ThemeDrawStyle := #Item.StatusColour
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(UpdateAccountInList) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#BPFUserAccountAdminItem) Name(#Item) Pass(*BY_REFERENCE)
    
    * Get Current Entry
    Get_Entry Number(#AccountList.FindReference( #Item ).Entry) From_List(#AccountList)
    
    If_Status Is(*OKAY)
    
    * Set the account icon
    #AccountListIcon.CurrentItem.Icon := #Item.IconName
    #AccountListIcon.CurrentItem.ThemeDrawStyle := #Item.StatusColour
    
    * Set Primary & Secondary Text
    #AccountListPrimaryText.CurrentItem.Caption := #Item.FirstName + ' ' + #Item.LastName
    #AccountListSecondaryText.CurrentItem.Caption := #Item.EmailAdress
    
    * Set Status Text
    #AccountListStatusText.CurrentItem.Visible := (#SYS_WEB.Device = Desktop)
    If (#AccountListStatusText.CurrentItem.Visible)
    
    #AccountListStatusText.CurrentItem.Caption := #Item.Status
    
    If (#Item.Status <> 'Active')
    
    #AccountListStatusText.CurrentItem.ThemeDrawStyle := #Item.StatusColour
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionForLoading) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#ClearList) Mandatory(True)
    
    If (#ClearList)
    
    * Clear
    Clr_List Named(#AccountList)
    
    Endif
    
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<ACCOUNTLISTACTIONLABEL.CAPTION.LOAD> )
    
    Endroutine
    
    Mthroutine Name(SetListActionAfterLoad)
    
    * If we have more results to load
    If (#AccountList.Items.ItemCount < #UserAccounts.UserAccounts.ItemCount)
    
    * We show loading more
    #COM_SELF.SetListActionDisplay( True #LoadMoreDisplayStyle "FOREGROUNDMEDIUMPRIMARY" #Strings.Text<ACCOUNTLISTACTIONLABEL.CAPTION.MORE> 'LOAD_MORE' )
    
    Endif
    
    * if we have no more results to load
    If (#AccountList.Items.ItemCount = #UserAccounts.UserAccounts.ItemCount)
    
    * Do we have more results on the server
    If (#MoreResults)
    
    * We show Refine search
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<ACCOUNTLISTACTIONLABEL.CAPTION.FULL> )
    
    Else
    
    If (#AccountList.Items.ItemCount > 0)
    
    * No more results on server, so no message needed
    #AccountListActionLabel.Visible := False
    
    Else
    
    * No Results found
    #COM_SELF.SetListActionDisplay( True *NULL *NULL #Strings.Text<ACCOUNTLISTACTIONLABEL.CAPTION.EMPTY> )
    
    Endif
    
    Endif
    
    Endif
    
    Endroutine
    
    Mthroutine Name(SetListActionDisplay) Access(*PRIVATE)
    Define_Map For(*INPUT) Class(#PRIM_BOLN) Name(#Visible)
    Define_Map For(*INPUT) Class(#prim_vs.Style) Name(#Style) Mandatory(*NULL) Pass(*BY_REFERENCE)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#DrawStyle) Mandatory(*NULL)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Message) Mandatory(*NULL)
    Define_Map For(*INPUT) Class(#PRIM_ALPH) Name(#Tag) Mandatory(*NULL)
    
    If (#Visible)
    
    * Set Style
    #AccountListActionLabel.Style <= #Style
    #AccountListActionLabel.ThemeDrawStyle := #DrawStyle
    
    * Set Text
    #AccountListActionLabel := #Message
    #AccountListActionLabel.ComponentTag := #Tag
    
    * Ensure we can see Action Label
    #AccountListActionLabel.Visible := True
    
    Else
    
    * If not visible then just hide
    #AccountListActionLabel.Visible := False
    
    Endif
    
    Endroutine
    
    Mthroutine Name(CollectFilterItems) Access(*PRIVATE)
    
    For Each(#control) In(#ListFilterTabs.ComponentControls) Operation(*INSTANCE_OF #PRIM_MD.TabItem)
    
    *
    If (#control.ComponentTag.LowerCase = #CurrentFilter.LowerCase)
    
    #control.View <= #Com_Self
    
    Endif
    
    #FilterItems.Insert( #control )
    
    Endfor
    
    Endroutine
    
    End_Com
